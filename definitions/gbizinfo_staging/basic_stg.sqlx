config {
  type: "view",
  schema: "gbizinfo_staging",
  tags: ["gbizinfo"],
  description: "基本情報",
  columns: {
    corporate_number: "法人番号",
    name: "法人名",
    kana: "法人名ふりがな",
    name_en: "法人名英語",
    postal_code: "郵便番号",
    location: "本社所在地",
    status: "ステータス",
    close_date: "登記記録の閉鎖等年月日",
    close_cause: "登記記録の閉鎖等の事由",
    representative_name: "法人代表者名",
    representative_position: "法人代表者役職",
    capital_stock: "資本金",
    employee_number: "従業員数",
    company_size_male: "企業規模詳細(男性)",
    company_size_female: "企業規模詳細(女性)",
    business_items: {
      description: "営業品目リスト",
      columns: {
        business_item_code: "営業品目コード値",
        business_item_name: "営業品目コード値(日本語)",
      },
    },
    business_summary: "事業概要",
    company_url: "企業ホームページ",
    date_of_establishment: "設立年月日",
    founding_year: "創業年",
    update_date: "最終更新日",
  },
}

SELECT
  IF(corporate_number <> "", corporate_number, ERROR('(corporate_number <> "") IS NOT TRUE')) AS corporate_number,
  NULLIF(name, "") AS name,
  NULLIF(kana, "") AS kana,
  NULLIF(name_en, "") AS name_en,
  CASE
    WHEN postal_code = "" THEN NULL
    WHEN REGEXP_CONTAINS(postal_code, "^[0-9]{7}$") THEN postal_code
    ELSE ERROR('postal_code IS NULL OR NOT REGEXP_CONTAINS(postal_code, "^[0-9]{7}$")')
  END AS postal_code,
  NULLIF(location, "") AS location,
  NULLIF(status, "") AS status,
  PARSE_DATE("%Y-%m-%d", NULLIF(close_date, "")) AS close_date,
  CASE close_cause
    WHEN "" THEN NULL
    WHEN "01" THEN "清算の結了等"
    WHEN "11" THEN "合併による解散等"
    WHEN "21" THEN "登記官による閉鎖"
    WHEN "31" THEN "その他の清算の結了等"
    ELSE ERROR('close_cause NOT IN ("", "01", "21", "31")')
  END AS close_cause,
  NULLIF(representative_name, "") AS representative_name,
  NULLIF(representative_position, "") AS representative_position,
  CAST(NULLIF(capital_stock, "") AS INT64) AS capital_stock,
  CAST(NULLIF(employee_number, "") AS INT64) AS employee_number,
  CAST(NULLIF(company_size_male, "") AS INT64) AS company_size_male,
  CAST(NULLIF(company_size_female, "") AS INT64) AS company_size_female,
  ARRAY(
    SELECT AS STRUCT
      business_item_code,
      CASE business_item_code
        WHEN "101" THEN "衣服・その他繊維製品類"
        WHEN "102" THEN "ゴム・皮革・プラスチック製品類"
        WHEN "103" THEN "窯業・土石製品類"
        WHEN "104" THEN "非鉄金属・金属製品類"
        WHEN "105" THEN "フォーム印刷"
        WHEN "106" THEN "その他印刷類"
        WHEN "107" THEN "図書類"
        WHEN "108" THEN "電子出版物類"
        WHEN "109" THEN "紙・紙加工品類"
        WHEN "110" THEN "車両類"
        WHEN "111" THEN "その他輸送・搬送機械器具類"
        WHEN "112" THEN "船舶類"
        WHEN "113" THEN "燃料類"
        WHEN "114" THEN "家具・什器類"
        WHEN "115" THEN "一般・産業用機器類"
        WHEN "116" THEN "電気・通信用機器類"
        WHEN "117" THEN "電子計算機類"
        WHEN "118" THEN "精密機器類"
        WHEN "119" THEN "医療用機器類"
        WHEN "120" THEN "事務用機器類"
        WHEN "121" THEN "その他機器類"
        WHEN "122" THEN "医薬品・医療用品類"
        WHEN "123" THEN "事務用品類"
        WHEN "124" THEN "土木・建設・建築材料"
        WHEN "127" THEN "警察用装備品類"
        WHEN "128" THEN "防衛用装備品類"
        WHEN "129" THEN "その他"
        WHEN "201" THEN "衣服・その他繊維製品類"
        WHEN "202" THEN "ゴム・皮革・プラスチック製品類"
        WHEN "203" THEN "窯業・土石製品類"
        WHEN "204" THEN "非鉄金属・金属製品類"
        WHEN "205" THEN "フォーム印刷"
        WHEN "206" THEN "その他印刷類"
        WHEN "207" THEN "図書類"
        WHEN "208" THEN "電子出版物類"
        WHEN "209" THEN "紙・紙加工品類"
        WHEN "210" THEN "車両類"
        WHEN "211" THEN "その他輸送・搬送機械器具類"
        WHEN "212" THEN "船舶類"
        WHEN "213" THEN "燃料類"
        WHEN "214" THEN "家具・什器類"
        WHEN "215" THEN "一般・産業用機器類"
        WHEN "216" THEN "電気・通信用機器類"
        WHEN "217" THEN "電子計算機類"
        WHEN "218" THEN "精密機器類"
        WHEN "219" THEN "医療用機器類"
        WHEN "220" THEN "事務用機器類"
        WHEN "221" THEN "その他機器類"
        WHEN "222" THEN "医薬品・医療用品類"
        WHEN "223" THEN "事務用品類"
        WHEN "224" THEN "土木・建設・建築材料"
        WHEN "225" THEN "造幣・印刷事業用原材料類"
        WHEN "226" THEN "造幣事業用金属工芸品類"
        WHEN "227" THEN "警察用装備品類"
        WHEN "228" THEN "防衛用装備品類"
        WHEN "229" THEN "その他"
        WHEN "301" THEN "広告・宣伝"
        WHEN "302" THEN "写真・製図"
        WHEN "303" THEN "調査・研究"
        WHEN "304" THEN "情報処理"
        WHEN "305" THEN "翻訳・通訳・速記"
        WHEN "306" THEN "ソフトウェア開発"
        WHEN "307" THEN "会場等の借り上げ"
        WHEN "308" THEN "賃貸借"
        WHEN "309" THEN "建物管理等各種保守管理"
        WHEN "310" THEN "運送"
        WHEN "311" THEN "車両整備"
        WHEN "312" THEN "船舶整備"
        WHEN "313" THEN "電子出版"
        WHEN "314" THEN "防衛用装備品類の整備"
        WHEN "315" THEN "その他"
        WHEN "401" THEN "立木竹"
        WHEN "402" THEN "その他"
        ELSE ERROR("Unsupported business_item_code: " || business_item_code)
      END AS business_item_name
    FROM
      UNNEST(SPLIT(NULLIF(business_items, ""), "、")) AS business_item_code WITH OFFSET
    ORDER BY
      OFFSET
  ) AS business_items,
  NULLIF(business_summary, "") AS business_summary,
  NULLIF(company_url, "") AS company_url,
  PARSE_DATE("%Y-%m-%d", NULLIF(date_of_establishment, "")) AS date_of_establishment,
  CAST(NULLIF(founding_year, "") AS INT64) AS founding_year,
  IF(update_date = "", NULL, IFNULL(SAFE.PARSE_DATE("%Y-%m-%dT00:00:00+09:00", update_date), PARSE_DATE("%Y-%m-%dT00:00:00Z", update_date))) AS update_date
FROM
  ${ref("gbizinfo_staging", "basic_raw")}
