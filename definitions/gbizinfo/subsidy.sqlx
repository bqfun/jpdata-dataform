config {
  type: "table",
  schema: "gbizinfo",
  description: "補助金情報",
  dependencies: ["subsidy_assert"],
  bigquery: {
    labels: {
      freshness: "daily",
    }
  },
  columns: {
    corporate_number: "法人番号",
    name: "法人名",
    location: "本社所在地",
    date_of_approval: "認定日",
    title: "補助金等",
    amount: "金額",
    target: "対象",
    government_departments: "府省",
    note: "備考",
    joint_signatures: "連名リスト",
    subsidy_resource: "補助金財源",
  },
}

SELECT
  IF(corporate_number <> "", corporate_number, ERROR('(corporate_number <> "") IS NOT TRUE')) AS corporate_number,
  IF(name <> "", name, ERROR('(name <> "") IS NOT TRUE')) AS name,
  NULLIF(location, "") AS location,
  PARSE_DATE("%Y-%m-%d", NULLIF(date_of_approval, "")) AS date_of_approval,
  NULLIF(title, "") AS title,
  CAST(NULLIF(amount, "") AS INT64) AS amount,
  NULLIF(target, "") AS target,
  IF(government_departments <> "", government_departments, ERROR('(government_departments <> "") IS NOT TRUE')) AS government_departments,
  NULLIF(note, "") AS note,
  SPLIT(NULLIF(joint_signatures, ""), "、") AS joint_signatures,
  NULLIF(subsidy_resource, "") AS subsidy_resource,
FROM
  ${ref("gbizinfo_staging", "subsidy")}
