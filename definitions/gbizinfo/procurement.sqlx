config {
  type: "table",
  schema: "gbizinfo",
  description: "調達情報",
  dependencies: ["procurement_assert"],
  bigquery: {
    labels: {
      freshness: "daily",
    }
  },
  columns: {
    corporate_number: "法人番号",
    name: "法人名",
    location: "本社所在地",
    date_of_order: "受注日",
    title: "事業名",
    amount: "金額",
    government_departments: "府省",
    joint_signatures: "連名リスト",
  },
}

SELECT
  IF(corporate_number <> "", corporate_number, ERROR('(corporate_number <> "") IS NOT TRUE')) AS corporate_number,
  IF(name <> "", name, ERROR('(name <> "") IS NOT TRUE')) AS name,
  NULLIF(location, "") AS location,
  PARSE_DATE("%Y-%m-%dT00:00:00+09:00", NULLIF(date_of_order, "")) AS date_of_order,
  NULLIF(title, "") AS title,
  CAST(NULLIF(amount, "") AS INT64) AS amount,
  IF(government_departments <> "", government_departments, ERROR('(government_departments <> "") IS NOT TRUE')) AS government_departments,
  SPLIT(NULLIF(joint_signatures, ""), "、") AS joint_signatures,
FROM
  ${ref("gbizinfo_staging", "procurement")}
