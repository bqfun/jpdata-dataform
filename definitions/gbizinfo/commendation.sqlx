config {
  type: "table",
  schema: "gbizinfo",
  description: "表彰情報",
  dependencies: ["commendation_assert"],
  bigquery: {
    labels: {
      freshness: "daily",
    }
  },
  columns: {
    corporate_number: "法人番号",
    name: "法人名",
    location: "本社所在地",
    date_of_commendation: "年月日",
    title: "表彰名",
    target: "受賞対象",
    category: "部門",
    government_departments: "府省",
  },
}

SELECT
  IF(corporate_number <> "", corporate_number, ERROR('(corporate_number <> "") IS NOT TRUE')) AS corporate_number,
  IF(name <> "", name, ERROR('(name <> "") IS NOT TRUE')) AS name,
  NULLIF(location, "") AS location,
  PARSE_DATE("%Y-%m-%d", NULLIF(date_of_commendation, "")) AS date_of_commendation,
  NULLIF(title, "") AS title,
  NULLIF(target, "") AS target,
  NULLIF(category, "") AS category,
  IF(government_departments <> "", government_departments, ERROR('(government_departments <> "") IS NOT TRUE')) AS government_departments,
FROM
  ${ref("gbizinfo_staging", "commendation")}
