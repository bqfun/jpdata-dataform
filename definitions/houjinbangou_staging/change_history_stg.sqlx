config {
  type: "view",
  schema: "houjinbangou_staging",
  description: "変更履歴情報",
  columns: {
    sequence_number: "一連番号",
    corporate_number: "法人番号",
    process: "処理区分",
    correct: "訂正区分",
    update_date: "更新年月日",
    change_date: "変更年月日",
    name: "商号又は名称",
    name_image_id: "商号又は名称イメージID",
    kind: "法人種別",
    prefecture_name: "国内所在地（都道府県）",
    city_name: "国内所在地（市区町村）",
    street_number: "国内所在地（丁目番地等）",
    address_image_id: "国内所在地イメージID",
    prefecture_code: "都道府県コード",
    city_code: "市区町村コード",
    post_code: "郵便番号",
    address_outside: "国外所在地",
    address_outside_image_id: "国外所在地イメージID",
    close_date: "登記記録の閉鎖等年月日",
    close_cause: "登記記録の閉鎖等の事由",
    successor_corporate_number: "承継先法人番号",
    change_cause: "変更事由の詳細",
    assignment_date: "法人番号指定年月日",
    latest: "最新履歴",
    en_name: "商号又は名称（英語表記）",
    en_prefecture_name: "国内所在地（都道府県）（英語表記）",
    en_city_name: "国内所在地（市区町村丁目番地等）（英語表記）",
    en_address_outside: "国外所在地（英語表記）",
    furigana: "フリガナ",
    hihyoji: "検索対象除外",
  },
}

WITH change_history AS (
  SELECT
    *
  FROM
    ${ref("houjinbangou_staging", "change_history_diff_stg")}
  UNION ALL
  SELECT
    *
  FROM
    ${ref("houjinbangou_staging", "change_history_num_before_20151201")} )
SELECT
  sequence_number,
  corporate_number,
  process,
  correct,
  update_date,
  change_date,
  name,
  name_image_id,
  kind,
  prefecture_name,
  city_name,
  street_number,
  address_image_id,
  prefecture_code,
  city_code,
  post_code,
  address_outside,
  address_outside_image_id,
  close_date,
  close_cause,
  successor_corporate_number,
  change_cause,
  assignment_date,
  ROW_NUMBER()OVER(PARTITION BY corporate_number ORDER BY update_date DESC, sequence_number DESC) = 1 AS latest,
  en_name,
  en_prefecture_name,
  en_city_name,
  en_address_outside,
  furigana,
  hihyoji,
FROM
  change_history
