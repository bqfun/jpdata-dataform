config {
  type: "assertion",
  schema: "houjinbangou_staging",
  disabled: true,
  tags: ["monthly"]
}

SELECT
  *
FROM
  ${ref("houjinbangou_staging", "latest_stg")} AS latest
LEFT JOIN
  (SELECT * FROM ${ref("houjinbangou_staging", "change_history_stg")} QUALIFY ROW_NUMBER()OVER(PARTITION BY corporate_number ORDER BY sequence_number DESC) = 1) AS change_history
USING(corporate_number)
WHERE
  -- latestにあって、change_historyにない
  change_history.corporate_number IS NULL
  -- latestより、change_historyが古い or どちらかが NULL
  OR (latest.update_date <= change_history.update_date) IS NOT TRUE
  OR (
    -- 同じupdate_dateだが、レコードが不一致
    latest.update_date = change_history.update_date
    AND (FORMAT("%T", latest) = FORMAT("%T", (SELECT AS STRUCT change_history.*EXCEPT(sequence_number, correct, latest)))) IS NOT TRUE
  )
