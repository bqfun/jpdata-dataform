config {
  type: "operations",
  schema: "shukujitsu",
  hasOutput: true,
}

CREATE OR REPLACE FUNCTION ${self()}(d DATE)
  AS (
    "1985-12-27" <= d
    AND EXTRACT(DAYOFWEEK FROM d) <> 1
    AND (
      ${ref("shukujitsu", "is_new_years_day")}(d - 1)
      OR ${ref("shukujitsu", "is_coming_of_age_day")}(d - 1)
      OR ${ref("shukujitsu", "is_national_foundation_day")}(d - 1)
      OR ${ref("shukujitsu", "is_emperors_birthday")}(d - 1)
      OR ${ref("shukujitsu", "is_vernal_equinox_day")}(d - 1)
      OR ${ref("shukujitsu", "is_showa_day")}(d - 1)
      OR ${ref("shukujitsu", "is_constitution_memorial_day")}(d - 1)
      OR ${ref("shukujitsu", "is_greenery_day")}(d - 1)
      OR ${ref("shukujitsu", "is_childrens_day")}(d - 1)
      OR ${ref("shukujitsu", "is_marine_day")}(d - 1)
      OR ${ref("shukujitsu", "is_mountain_day")}(d - 1)
      OR ${ref("shukujitsu", "is_respect_for_the_aged_day")}(d - 1)
      OR ${ref("shukujitsu", "is_autumnal_equinox_day")}(d - 1)
      OR ${ref("shukujitsu", "is_health_sports_day")}(d - 1)
      OR ${ref("shukujitsu", "is_sports_day")}(d - 1)
      OR ${ref("shukujitsu", "is_culture_day")}(d - 1)
      OR ${ref("shukujitsu", "is_labour_thanksgiving_day")}(d - 1)
      OR ${ref("shukujitsu", "extra_holiday_name")}(d - 1) IS NOT NULL
    )
    AND (
      ${ref("shukujitsu", "is_new_years_day")}(d + 1)
      OR ${ref("shukujitsu", "is_coming_of_age_day")}(d + 1)
      OR ${ref("shukujitsu", "is_national_foundation_day")}(d + 1)
      OR ${ref("shukujitsu", "is_emperors_birthday")}(d + 1)
      OR ${ref("shukujitsu", "is_vernal_equinox_day")}(d + 1)
      OR ${ref("shukujitsu", "is_showa_day")}(d + 1)
      OR ${ref("shukujitsu", "is_constitution_memorial_day")}(d + 1)
      OR ${ref("shukujitsu", "is_greenery_day")}(d + 1)
      OR ${ref("shukujitsu", "is_childrens_day")}(d + 1)
      OR ${ref("shukujitsu", "is_marine_day")}(d + 1)
      OR ${ref("shukujitsu", "is_mountain_day")}(d + 1)
      OR ${ref("shukujitsu", "is_respect_for_the_aged_day")}(d + 1)
      OR ${ref("shukujitsu", "is_autumnal_equinox_day")}(d + 1)
      OR ${ref("shukujitsu", "is_health_sports_day")}(d + 1)
      OR ${ref("shukujitsu", "is_sports_day")}(d + 1)
      OR ${ref("shukujitsu", "is_culture_day")}(d + 1)
      OR ${ref("shukujitsu", "is_labour_thanksgiving_day")}(d + 1)
      OR ${ref("shukujitsu", "extra_holiday_name")}(d + 1) IS NOT NULL
    )
  )
  OPTIONS (
    description = "前日と翌日の両方を「国民の祝日」に挟まれた平日は休日とする"
  )
